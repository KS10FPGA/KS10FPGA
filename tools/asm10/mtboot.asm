
;
; Boot the KS10 from Magtape
;
;  The first tape file is microcode. Skip over the microcode.
;  The second tape file is MTBOOT. Read MTBOOT and jump to address o1000 to
;    start MTBOOT.
;

	LOC	377000

	;
	; SET BOOT ADDRESS
	;

	START	MTBOOT

	;
	; AC0 = DATA
	; AC1 = ADDR

MTBOOT:

	;
	; SETUP UBA PAGING
	;

	HLLZ  1, 000036		; GET UBA# FROM CONSOLE DATA AREA
	MOVEI 0, 140001		; FTM, VALID, PAGE 1
	WRIO  0, 763001(1)	; WRITE PAGE 1

	;
	; CLEAR UBA
	;

	MOVE  1, 000036		; GET RH11 BASE ADDR FROM CONSOLE DATA AREA
	;MOVEI 0, 000100	; UBACSR[INI]
	;WRIO  0, 100(1)	; WRITE TO UBACSR

	;
	; DEVICE CLEAR
	;

	MOVEI 0, 000040		; RHCS2[CLR]
	WRIO  0, 10(1)		; WRITE TO MTCS2

	;
	; SET UNIT
	;

	MOVE  0, 000037		; GET UNIT FROM CONSOLE DATA AREA
	WRIO  0, 10(1)		; WRITE TO MTCS2

	;
	; SEE IF WE ARE AT BOT.	 IF NOT, REWIND THE TAPE
	;

	RDIO  0, 12(1)		; READ MTDS
	TRNE  0, 000002		; CHECK BEGINNING-OF-TAPE (MTDS[BOT])
	JRST  NOWREW		; SKIP REWIND IF BOT
	MOVEI 2, 7		; REWIND COMMAND
	JSP   17, DOCMD		; EXECUTE COMMAND

	;
	; SPACE FORWARD OVER MICROCODE
	;

NOREW:	MOVEI 2, 31		; SPACE FORWARD COMMAND
	JSP   17, DOCMD		; EXECUTE COMMAND

	;
	; READ BOOT
	;

	MOVEI 2, 71		; READ FORWARD
	JSP   17, DOCMD		; EXECUTE COMMAND

	;
	; EXECUTE BOOT
	;

	JRST	 1000		; JUMP TO BOOTLOADER

	;
	; SUBROUTINE TO RUN A COMMAND
	;
	;  AC2 IS THE COMMAND
	;  AC1 IS THE BASE ADDR
	;  AC0 IS TEMP DATA

	;
	; CONFIGURE MTTC
	;

DOCMD:	MOVE  0, 000040		; GET MTPARAM FROM CONSOLE DATA AREA
	ANDI  0, 003767		; KEEP DEN, FMT, AND SS
	WRIO  0, 32(1)		; WRITE TO MTTC

	;
	; ISSUE AN DRIVE CLEAR COMMAND TO MTCS1
	;

	MOVEI 0, 000011		; DRV CLEAR
	WRIO  0, 0(1)		; WRITE TO MTCS1

	;
	; SET WORD COUNT
	;

	MOVEI 0, 176000		; 128 WORDS
	WRIO  0, 2(1)		; WRITE TO MTWC

	;
	; SET BASE ADDRESS
	;

	MOVEI 0, 004000		; 04000
	WRIO  0, 4(1)		; WRITE TO MTBA

	;
	; SET FRAME COUNT
	;

	MOVEI 0, 0		; 0
	WRIO  0, 6(1)		; WRITE TO MTFC

	;
	; EXECUTE COMMAND
	;

	WRIO  2, 0(1)		; WRITE TO MTCS1

	;
	; CHECK MTDS STATUS
	;

TSTDRY: RDIO  0, 12(1)		; READ MTDS
	TRNN  0, 200		; TEST DATA READY (MTDS[DRY])
	JRST  TSTDRY		; CHECK AGAIN
	TRNE  0, 40000		; TEST ERR
	HALT  .			; HALT HERE

	;
	; CLEAR MTDS[ATA]
	;  (WORKS FOR UNIT 0 ONLY)

	MOVEI 0, 1		; INIT AC
	WRIO  0, 16(1)		; WRITE TO MTAS

	;
	; IF REWIND COMMAND, WAIT FOR COMMAND TO COMPLETE
	;

	ANDI  2, 000076		; MASK FUNCTION
	CAIE  2, 000006		; SKIP IF REWIND COMMAND
	JRST  NOTREW		; NOT REWIND
TSTATA:	RDIO  0, 12(1)		; READ MTDS
	TRNN  0, 100000		; TEST ATTENTION (MTDS[ATA])
	JRST  TSTATA

	;
	; CLEAR MTDS[ATA]
	;  (WORKS FOR UNIT 0 ONLY)

	MOVEI 0, 1		; INIT AC
	WRIO  0, 16(1)		; WRITE TO MTAS

	;
	; CHECK MTER STATUS2
	;

NOTREW:	RDIO  0, 14(1)		; READ MTER
	CAIN  0, 1000		; TEST FRAME CHECK ERROR (MTER[FCE])
	HALT  .			; HALT HERE
	JRST  0(17)		; RETURN
